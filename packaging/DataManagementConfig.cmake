cmake_minimum_required(VERSION 3.19)

set(DataManagement_known_comps static shared)
set(DataManagement_comp_static NO)
set(DataManagement_comp_shared NO)
foreach (DataManagement_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (DataManagement_comp IN_LIST DataManagement_known_comps)
        set(DataManagement_comp_${DataManagement_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "DataManagement does not recognize component `${DataManagement_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (DataManagement_comp_static AND DataManagement_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "DataManagement `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(DataManagement_static_targets "${CMAKE_CURRENT_LIST_DIR}/DataManagement-static-targets.cmake")
set(DataManagement_shared_targets "${CMAKE_CURRENT_LIST_DIR}/DataManagement-shared-targets.cmake")

macro(DataManagement_load_targets type)
    if (NOT EXISTS "${DataManagement_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "DataManagement `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${DataManagement_${type}_targets}")
endmacro()

if (DataManagement_comp_static)
    DataManagement_load_targets(static)
elseif (DataManagement_comp_shared)
    DataManagement_load_targets(shared)
elseif (DEFINED DataManagement_SHARED_LIBS AND DataManagement_SHARED_LIBS)
    DataManagement_load_targets(shared)
elseif (DEFINED DataManagement_SHARED_LIBS AND NOT DataManagement_SHARED_LIBS)
    DataManagement_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${DataManagement_shared_targets}")
        DataManagement_load_targets(shared)
    else ()
        DataManagement_load_targets(static)
    endif ()
else ()
    if (EXISTS "${DataManagement_static_targets}")
        DataManagement_load_targets(static)
    else ()
        DataManagement_load_targets(shared)
    endif ()
endif ()